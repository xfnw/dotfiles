#!/usr/bin/env nu

def htmlenc [] { str replace -a "&" "&amp;" | str replace -a "<" "&lt;" }
def bugpage [id] {
let b = git bug show -f json $id | from json
let top = $"<!DOCTYPE html>
<meta charset='utf-8'>
<meta name='viewport' content='width=device-width, initial-scale=1'>
<style>html{max-width:60em;margin:0 auto;color-scheme:dark light}:target{border:1px solid}pre,html{font-family:monospace,monospace}pre{white-space:pre-wrap}@media only print{nav{display:none}}</style>
<title>($b.title | htmlenc) - gbstatic</title>
<nav><a href='../index.html'>back</a></nav>
<h1>($b.human_id) [($b.status)] ($b.title | htmlenc)</h1>
<dt>author</dt><dd>($b.author.name | htmlenc) \(($b.author.human_id)\) (if not ($b.author.login | is-empty) { '@' })($b.author.login | htmlenc)</dd>
<dt>created</dt><dd>($b.create_time.time)</dd>
<dt>active</dt><dd>($b.edit_time.time)</dd>
<dt>labels</dt><dd>(if ($b.labels | is-empty) { '<em>none</em>' }
else { $b.labels | str join ', ' | htmlenc })</dd>

<h3>comments \(($b.comments | length)\)</h3>
"
let comments = $b.comments | each {|c|
$"<small id='($c.human_id)'><a href='#($c.human_id)'>($c.human_id)</a> by ($c.author.name | htmlenc) \(($c.author.human_id)\) (if not ($b.author.login | is-empty) { '@' })($c.author.login | htmlenc)</small>
<blockquote><pre>($c.message | htmlenc)</pre></blockquote>"
} | str join

[$top $comments]
}

# FIXME: no clue why, but this seems to make the thread dir
# have 777 permissions?
mkdir thread

let bugs = git bug ls -f json |
from json |
sort-by -r edit_time |
each {|e|
bugpage $e.id | save -f --raw $"thread/($e.id).html"
$"<a href='thread/($e.id).html'>($e.human_id) [($e.status)] ($e.title | htmlenc)</a>"
} | str join "\n"

["<!DOCTYPE html>
<meta charset='utf-8'>
<meta name='viewport' content='width=device-width, initial-scale=1'>
<style>html{max-width:60em;margin:0 auto;color-scheme:dark light}pre,html{font-family:monospace,monospace}pre{white-space:pre-wrap}</style>
<title>bugs - gbstatic</title>
<h1>bugs</h1>
<p>static mirror from git-bug's json export, some context will be missing.
<pre>" $bugs "</pre>"] | save -f --raw index.html

