#!/usr/bin/env nu

def htmlenc [] { str replace -a "&" "&amp;" | str replace -a "<" "&lt;" }
def bugpage [id] {
let b = git bug show -f json $id | from json
let top = $"<!DOCTYPE html>
<style>html{max-width:60em;margin:0 auto}:target{background:#aa6}</style>
<title>($b.title | htmlenc) - gbstatic</title>
<a href='../index.html'>back</a>
<h1>($b.human_id) [($b.status)] ($b.title | htmlenc)</h1>
<dt>author</dt><dd>($b.author.name | htmlenc) \(($b.author.human_id)\)</dd>
<dt>created</dt><dd>($b.create_time.time)</dd>
<dt>active</dt><dd>($b.edit_time.time)</dd>

<h3>comments \(($b.comments | length)\)</h3>
"
let comments = $b.comments | each {|c|
$"<small id='($c.human_id)'><a href='#($c.human_id)'>($c.human_id)</a> by ($c.author.name | htmlenc) \(($c.author.human_id)\)</small>
<blockquote>($c.message | htmlenc | str replace -a "\n" "<br>\n")</blockquote>"
} | str join "<br>"

[$top, $comments] | str join "<br>"
}

# FIXME: no clue why, but this seems to make the thread dir
# have 777 permissions?
mkdir thread

let bugs = git bug ls -f json |
from json |
sort-by -r edit_time |
each {|e|
bugpage $e.id | save -f --raw $"thread/($e.id).html"
$"<a href='thread/($e.id).html'>($e.human_id) [($e.status)] ($e.title | htmlenc)</a>"
} | str join "\n"

["<!DOCTYPE html>
<style>html{max-width:60em;margin:0 auto}</style>
<title>bugs - gbstatic</title>
<h1>bugs</h1>
<p>static mirror of git-bug, please submit issues via it instead.
<pre>", $bugs, "</pre>"] | save -f --raw index.html

